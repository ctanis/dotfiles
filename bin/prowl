#!/usr/bin/perl -w

use strict;
use Getopt::Long;
use Pod::Usage;
use Sys::Hostname;

# Grab our options.
my %options = ();
GetOptions(\%options, 'apikey=s', 'apikeyfile=s',
					  'providerkey=s', 'url=s',
					  'application=s', 'event=s', 'notification=s',
                                          'priority:i', 'help|?') or pod2usage(2);

my $hn= hostname;
$hn =~ s/^([^.]+)\..*/$1/;

$options{'application'} ||= $hn . '';
$options{'priority'} ||= 0;
$options{'url'} ||= "";
$options{'event'} ||= ` `;

pod2usage(-verbose => 2) if (exists($options{'help'}));
pod2usage(-message => "$0: Event name is required") if (!exists($options{'event'}));
pod2usage(-priority => "$0: Priority must be in the range [-2, 2]") if ($options{'priority'} < -2 || $options{'priority'} > 2);

# default apikeyfile location
if (! exists($options{'apikey'}) && !exists($options{'apikeyfile'}))  {
  $options{'apikeyfile'} = $ENV{HOME}.'/.prowl_key';
}


# Get the API key from STDIN if one isn't provided via a file or from the command line.
if (!exists($options{'apikey'}) && !exists($options{'apikeyfile'})) {
  die "No API Key";
} elsif (exists($options{'apikeyfile'})) {
	open(APIKEYFILE, $options{'apikeyfile'}) or die($!);
	$options{'apikey'} = <APIKEYFILE>;
	close(APIKEYFILE);
	
	chomp $options{'apikey'};
}

unless ($options{notification}) {
  while (<STDIN>) {
    $options{notification} .= $_;
  }
}

pod2usage(-message => "$0: Notification text is required") if (!exists($options{'notification'}));


# URL encode our arguments
$options{'application'} =~ s/([^A-Za-z0-9])/sprintf("%%%02X", ord($1))/seg;
$options{'event'} =~ s/([^A-Za-z0-9])/sprintf("%%%02X", ord($1))/seg;
$options{'notification'} =~ s/([^A-Za-z0-9])/sprintf("%%%02X", ord($1))/seg;

my $providerKeyString = "";
if(exists($options{'providerkey'})) {
	$providerKeyString = sprintf("&providerkey=%s", $options{'providerkey'});
}

my $requestURL = sprintf("https://prowlapp.com/publicapi/add?apikey=%s&application=%s&event=%s&description=%s&priority=%d&url=%s%s",
				$options{'apikey'},
				$options{'application'},
				$options{'event'},
				$options{'notification'},
				$options{'priority'},
				$options{'url'},
				$providerKeyString);

my $Z=join ' ', `curl '$requestURL' 2>/dev/null`;

if ($Z =~ /success/i) {
  print "Notification posted\n";
} else {
  print STDERR "Error\n";
}

__END__

=head1 NAME 

prowl - Send Prowl notifications

=head1 SYNOPSIS

prowl.pl [options] event_information

 Options:
   -help              Display all help information.
   -apikey=...        Your Prowl API key.
   -apikeyfile=...    A file containing your Prowl API key.
   -providerkey=...   Your provider key (optional)

 Event information:
   -application=...   The name of the application.
   -event=...         The name of the event.
   -notification=...  The text of the notification.
   -priority=...      The priority of the notification.
                      An integer in the range [-2, 2].
   -url=...			  The URL to attach to the notification, and possibly redirect to.

=head1 OPTIONS

=over 8

=item B<-apikey>

Your Prowl API key. It is not recommend you use this, use the apikeyfile option.

=item B<-providerkey>

Your Prowl provider key. This is an optional argument, and should be used if you were provided one for whitelisting reasons.

=item B<-apikeyfile>

A file containing one line, which has your Prowl API key on it.

=item B<-application>

The name of the Application part of the notification. If none is provided, ProwlScript is used.

=item B<-event>

The name of the Event part of the notification. This is generally the action which occurs, such as "disk partitioning completed."

=item B<-notification>

The text of the notification, which has more details for a particular event. This is generally the description of the action which occurs, such as "The disk /dev/abc was successfully partitioned."

=item B<-priority>

The priority level of the notification. An integer value ranging [-2, 2] with meanings Very Low, Moderate, Normal, High, Emergency.

=item B<-url>

The URL which should be attached to the notification. Prowl will either ask, or immediately redirect, to the new URL depending on the user's settings. The user can always access the attached URL from within the application afterwards.

=back

=head1 DESCRIPTION

B<This program> sends a notification to the Prowl server, which is then forwarded to your device running the Prowl application.

=head1 HELP

For more assistance, visit the Prowl website at <http://prowlapp.com>.

=cut
